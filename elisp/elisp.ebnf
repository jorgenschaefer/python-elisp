file = @:{expression}* $;

expression = list | vector
           | integer | float | symbol | string;

integer = ?/[+-]?[0-9]+\.?/? !symbol;

float = ?/[+-]?[0-9]*\.[0-9]+e[0-9]+/? !symbol
      | ?/[+-]?[0-9]+e[0-9]+/? !symbol
      | ?/[+-]?[0-9]*\.[0-9]+/? !symbol
      ;

(* Character syntax not supported *)

(* Symbols can contain periods, but a single period is not a symbol *)
symbol = ?/([-+=*/_~!@$%^&:<>{}?A-Za-z0-9.]+|\\.){2,}/?
       | ?/([-+=*/_~!@$%^&:<>{}?A-Za-z0-9]+|\\.)/?;

list = proper_list | improper_list;
proper_list = '(' @+:{expression}* ')';
improper_list = '(' @:({expression}+ '.' expression) ')';

(* Does not support the complex \C-a syntax *)
string = '"' @:?/([^"]|\n)*|(\\(.|\n))/?  '"';

vector = '[' @+:{expression}* ']';

(* Char-Table not supported *)
(* Bool-Vector not supported *)
(* Hash Table not supported *)
(* Byte-Code not supported *)
